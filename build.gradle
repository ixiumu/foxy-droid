buildscript {
  ext.versions = [
    android: '7.3.0',
    kotlin: '1.6.0'
  ]

  repositories {
    mavenCentral()
    google()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:' + versions.android
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:' + versions.kotlin
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion 33

  defaultConfig {
    archivesBaseName = 'iDroid'
    applicationId 'com.idroid'
    minSdkVersion 21
    targetSdkVersion 33
    versionCode 4
    versionName '1.3'

    def languages = [ 'en', 'zh-rCN' ]
    buildConfigField 'String[]', 'LANGUAGES', '{ "' + languages.join('", "') + '" }'
    resConfigs languages
  }

  sourceSets.all {
    def javaDir = it.java.srcDirs.find { it.name == 'java' }
    it.java.srcDirs += new File(javaDir.parentFile, 'kotlin')
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = compileOptions.sourceCompatibility.toString()
  }

  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      buildConfigField "String", "REPO_URL", '"https://f-droid.org/repo"'
      buildConfigField "String", "REPO_ARCHIVE_URL", '"https://f-droid.org/archive"'
    }

    release {
      minifyEnabled true
      shrinkResources false
      buildConfigField "String", "REPO_URL", '"https://cloudflare.f-droid.org/repo"'
      buildConfigField "String", "REPO_ARCHIVE_URL", '"https://cloudflare.f-droid.org/archive"'
    }

    cnRelease {
      minifyEnabled true
      shrinkResources false
      buildConfigField "String", "REPO_URL", '"https://mirrors.tuna.tsinghua.edu.cn/fdroid/repo"'
      buildConfigField "String", "REPO_ARCHIVE_URL", '"https://mirrors.tuna.tsinghua.edu.cn/fdroid/archive"'
    }

    all {
      crunchPngs false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
    }
  }

  lintOptions {
    warning 'InvalidPackage'
    ignore 'InvalidVectorPath'
  }

  packagingOptions {
    exclude '/DebugProbesKt.bin'
    exclude '/kotlin/**.kotlin_builtins'
    exclude '/kotlin/**.kotlin_metadata'
    exclude '/META-INF/**.kotlin_module'
    exclude '/META-INF/**.pro'
    exclude '/META-INF/**.version'
    exclude '/okhttp3/internal/publicsuffix/*'
  }

  def keystorePropertiesFile = rootProject.file('keystore.properties')
  if (keystorePropertiesFile.exists()) {
    def keystoreProperties = new Properties()
    keystoreProperties.load(keystorePropertiesFile.newDataInputStream())

    def signing = [
        storeFile: keystoreProperties['storeFile'],
        storePassword: keystoreProperties['storePassword'],
        keyAlias: keystoreProperties['keyAlias'],
        keyPassword: keystoreProperties['keyPassword']
    ]

    if (!signing.any { _, v -> v == null }) {
      signingConfigs {
        primary {
          storeFile file(signing.storeFile)
          storePassword signing.storePassword
          keyAlias signing.keyAlias
          keyPassword signing.keyPassword
          v2SigningEnabled true
        }
      }

      buildTypes {
        debug.signingConfig signingConfigs.primary
        release.signingConfig signingConfigs.primary
      }
    }
  }
}

repositories {
  mavenCentral()
  google()
  maven { url 'https://jitpack.io' }
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:' + versions.kotlin
  implementation 'androidx.fragment:fragment-ktx:1.5.6'
  implementation 'androidx.viewpager2:viewpager2:1.0.0'
  implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
  implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
  implementation 'io.reactivex.rxjava3:rxjava:3.1.1'
  implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
  implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'
  implementation 'com.fasterxml.jackson.core:jackson-core:2.14.2'
  implementation 'com.squareup.picasso:picasso:2.8'
  implementation 'com.github.topjohnwu.libsu:core:5.0.4'
}